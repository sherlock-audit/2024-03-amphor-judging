Glorious Purple Ape

medium

# Incorrect assignment of snapshot values in `AsyncSynthVault.previewSettle()`.

## Summary

In `AsyncSynthVault.previewSettle()`, `totalAssetsSnapshotForDeposit, totalSupplySnapshotForDeposit, totalAssetsSnapshotForRedeem`, and `totalSupplySnapshotForRedeem` all increase by 1 more than they should.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L626-L684

```javascript
    function previewSettle(uint256 newSavedBalance)
        public
        view
        returns (
            uint256 assetsToOwner,
            uint256 assetsToVault,
            SettleValues memory settleValues
        )
    {
        uint256 _lastSavedBalance = lastSavedBalance;
        _checkMaxDrawdown(_lastSavedBalance, newSavedBalance);

        // calculate the fees between lastSavedBalance and newSavedBalance
        uint256 fees = _computeFees(_lastSavedBalance, newSavedBalance);
        uint256 totalSupply = totalSupply();

        // taking fees if positive yield
        _lastSavedBalance = newSavedBalance - fees;

        address pendingSiloAddr = address(pendingSilo);
        uint256 pendingRedeem = balanceOf(pendingSiloAddr);
        uint256 pendingDeposit = _asset.balanceOf(pendingSiloAddr);

        uint256 sharesToMint = pendingDeposit.mulDiv(
            totalSupply + 1, _lastSavedBalance + 1, Math.Rounding.Floor
        );

653     uint256 totalAssetsSnapshotForDeposit = _lastSavedBalance + 1;
663     uint256 totalSupplySnapshotForDeposit = totalSupply + 1;

        uint256 assetsToWithdraw = pendingRedeem.mulDiv(
            _lastSavedBalance + pendingDeposit + 1,
            totalSupply + sharesToMint + 1,
            Math.Rounding.Floor
        );

        uint256 totalAssetsSnapshotForRedeem =
663         _lastSavedBalance + pendingDeposit + 1;
664     uint256 totalSupplySnapshotForRedeem = totalSupply + sharesToMint + 1;

        settleValues = SettleValues({
            lastSavedBalance: _lastSavedBalance + fees,
            fees: fees,
            pendingRedeem: pendingRedeem,
            sharesToMint: sharesToMint,
            pendingDeposit: pendingDeposit,
            assetsToWithdraw: assetsToWithdraw,
            totalAssetsSnapshotForDeposit: totalAssetsSnapshotForDeposit,
            totalSupplySnapshotForDeposit: totalSupplySnapshotForDeposit,
            totalAssetsSnapshotForRedeem: totalAssetsSnapshotForRedeem,
            totalSupplySnapshotForRedeem: totalSupplySnapshotForRedeem
        });

        if (pendingDeposit > assetsToWithdraw) {
            assetsToOwner = pendingDeposit - assetsToWithdraw;
        } else if (pendingDeposit < assetsToWithdraw) {
            assetsToVault = assetsToWithdraw - pendingDeposit;
        }
    }
```

At lines 653, 654, 663, and 664, snapshot values of `totalSupply` and `totalAssets` are set. However, they are all incremented by 1 more than they should be. These discrepancies impact the exchange rates through [AsyncSynthVault._convertToShares(), AsyncSynthVault._convertToAssets()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L875-L911).

## Impact

The exchange rate for the previous epochId is slightly altered.

## Code Snippet

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L626-L684

## Tool used

Manual Review

## Recommendation

In `AsyncSynthVault.previewSettle()`, L653, L654, L663 and L664 should be fixed as follows.

```diff
    function previewSettle(uint256 newSavedBalance)
        public
        view
        returns (
            uint256 assetsToOwner,
            uint256 assetsToVault,
            SettleValues memory settleValues
        )
    {
        uint256 _lastSavedBalance = lastSavedBalance;
        _checkMaxDrawdown(_lastSavedBalance, newSavedBalance);

        // calculate the fees between lastSavedBalance and newSavedBalance
        uint256 fees = _computeFees(_lastSavedBalance, newSavedBalance);
        uint256 totalSupply = totalSupply();

        // taking fees if positive yield
        _lastSavedBalance = newSavedBalance - fees;

        address pendingSiloAddr = address(pendingSilo);
        uint256 pendingRedeem = balanceOf(pendingSiloAddr);
        uint256 pendingDeposit = _asset.balanceOf(pendingSiloAddr);

        uint256 sharesToMint = pendingDeposit.mulDiv(
            totalSupply + 1, _lastSavedBalance + 1, Math.Rounding.Floor
        );

-       uint256 totalAssetsSnapshotForDeposit = _lastSavedBalance + 1;
-       uint256 totalSupplySnapshotForDeposit = totalSupply + 1;
+       uint256 totalAssetsSnapshotForDeposit = _lastSavedBalance;
+       uint256 totalSupplySnapshotForDeposit = totalSupply;

        uint256 assetsToWithdraw = pendingRedeem.mulDiv(
            _lastSavedBalance + pendingDeposit + 1,
            totalSupply + sharesToMint + 1,
            Math.Rounding.Floor
        );

-       uint256 totalAssetsSnapshotForRedeem =
-           _lastSavedBalance + pendingDeposit + 1;
-       uint256 totalSupplySnapshotForRedeem = totalSupply + sharesToMint + 1;
+       uint256 totalAssetsSnapshotForRedeem =
+           _lastSavedBalance + pendingDeposit;
+       uint256 totalSupplySnapshotForRedeem = totalSupply + sharesToMint;

        settleValues = SettleValues({
            lastSavedBalance: _lastSavedBalance + fees,
            fees: fees,
            pendingRedeem: pendingRedeem,
            sharesToMint: sharesToMint,
            pendingDeposit: pendingDeposit,
            assetsToWithdraw: assetsToWithdraw,
            totalAssetsSnapshotForDeposit: totalAssetsSnapshotForDeposit,
            totalSupplySnapshotForDeposit: totalSupplySnapshotForDeposit,
            totalAssetsSnapshotForRedeem: totalAssetsSnapshotForRedeem,
            totalSupplySnapshotForRedeem: totalSupplySnapshotForRedeem
        });

        if (pendingDeposit > assetsToWithdraw) {
            assetsToOwner = pendingDeposit - assetsToWithdraw;
        } else if (pendingDeposit < assetsToWithdraw) {
            assetsToVault = assetsToWithdraw - pendingDeposit;
        }
    }
```
