Tart Raisin Cormorant

high

# Functions used to convert between assets and shares for settled epochs are incorrect

## Summary

Both `convertToShares()` and `convertToAssets()` incorrectly add 1 to the stored settled values, leading to a discrepancy between the expected amounts of assets and shares calculated when settling and the effective amounts when requests are claimed. This difference can potentially cause a denial of service and prevent requests from being claimed due to a lack of value in the claimable silo.

## Vulnerability Detail

The logic to convert between assets and shares is given by the base implementation in SyncSynthVault. This contract presumably follows the OpenZeppelin implementation and defines `convertToShares()` and `convertToAssets()` as the following, note that assets and supply are incremented by 1.

```solidity
function _convertToShares(
    uint256 assets,
    Math.Rounding rounding
)
    internal
    view
    returns (uint256)
{
    return assets.mulDiv(totalSupply() + 1, totalAssets() + 1, rounding);
}

function _convertToAssets(
    uint256 shares,
    Math.Rounding rounding
)
    internal
    view
    returns (uint256)
{
    return shares.mulDiv(totalAssets() + 1, totalSupply() + 1, rounding);
}
```

The implementation of AsyncSynthVault tries to replicate this logic when an epoch is settled. This can be seen in `previewSettle()`, which calculates all the required values to settle an epoch:

```solidity
uint256 sharesToMint = pendingDeposit.mulDiv(
    totalSupply + 1, _lastSavedBalance + 1, Math.Rounding.Floor
);

uint256 totalAssetsSnapshotForDeposit = _lastSavedBalance + 1;
uint256 totalSupplySnapshotForDeposit = totalSupply + 1;

uint256 assetsToWithdraw = pendingRedeem.mulDiv(
    _lastSavedBalance + pendingDeposit + 1,
    totalSupply + sharesToMint + 1,
    Math.Rounding.Floor
);

uint256 totalAssetsSnapshotForRedeem =
    _lastSavedBalance + pendingDeposit + 1;
uint256 totalSupplySnapshotForRedeem = totalSupply + sharesToMint + 1;
```

Note how the implementation adds 1 to the total supply and total assets, and then stores these snapshotted values so that they can be used later when a request gets claimed. However, when this happens, the implementation **adds 1 again to the stored values** when calculating the effective amounts during claims:

```solidity
function _convertToShares(
    uint256 assets,
    uint256 requestId,
    Math.Rounding rounding
)
    internal
    view
    returns (uint256)
{
    if (isCurrentEpoch(requestId)) {
        return 0;
    }
    uint256 totalAssets =
        epochs[requestId].totalAssetsSnapshotForDeposit + 1;
    uint256 totalSupply =
        epochs[requestId].totalSupplySnapshotForDeposit + 1;

    return assets.mulDiv(totalSupply, totalAssets, rounding);
}

function _convertToAssets(
    uint256 shares,
    uint256 requestId,
    Math.Rounding rounding
)
    internal
    view
    returns (uint256)
{
    if (isCurrentEpoch(requestId)) {
        return 0;
    }
    uint256 totalAssets = epochs[requestId].totalAssetsSnapshotForRedeem + 1;
    uint256 totalSupply = epochs[requestId].totalSupplySnapshotForRedeem + 1;

    return shares.mulDiv(totalAssets, totalSupply, rounding);
}
```

This can potentially generate a fundamental difference in which the assets or shares calculated when settling the epoch are not the same as the effective assets or shares calculated when claiming a request. If the values calculated when settling are less than the values calculated when claiming, then claims will eventually fail because the assets or shares present in the claimable silo are not enough.

## Proof of concept

The following proof of concept is an example of such a situation, in which a user cannot claim their deposit because the number of shares present in the claimable silo is less than the number of shares calculated during the claim:

```solidity
function test_InsufficientShares() public {
    asset.mint(alice, 1_000e18);
    asset.mint(bob, 10_000e18);
    asset.mint(owner, 1_000e18);

    vm.prank(alice);
    vault.deposit(1_000e18, alice);

    // vault is closed
    vm.prank(owner);
    vault.close();

    // bob requests a deposit
    vm.prank(bob);
    vault.requestDeposit(10_000e18, bob, bob, "");

    // owner opens
    vm.prank(owner);
    vault.open(2_000e18);

    // Minted shares in claimable silo are not enough to cover Bob's request
    uint256 bobShares = vault.convertToShares(10_000e18, vault.lastDepositRequestId(bob));
    uint256 claimableSiloShares = vault.totalClaimableShares();
    assertLt(claimableSiloShares, bobShares);

    // bob can't claim his deposit, the transaction will revert
    vm.prank(bob);
    vm.expectRevert();
    vault.claimDeposit(bob);
}
```

## Impact

Discrepancies between the number of assets or shares calculated when settling an epoch, and later when requests are claimed, could cause a lack of funds in the claimable silo, preventing claiming and potentially bricking requests.

## Code Snippet

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L875-L893

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L895-L911

## Tool used

Manual Review

## Recommendation

Remove the `+ 1` in `_convertToShares()` and `_convertToAssets()`, since the stored snapshotted values already account for it.

```diff
    function _convertToShares(
        uint256 assets,
        uint256 requestId,
        Math.Rounding rounding
    )
        internal
        view
        returns (uint256)
    {
        if (isCurrentEpoch(requestId)) {
            return 0;
        }
        uint256 totalAssets =
-           epochs[requestId].totalAssetsSnapshotForDeposit + 1;
+           epochs[requestId].totalAssetsSnapshotForDeposit;
        uint256 totalSupply =
-           epochs[requestId].totalSupplySnapshotForDeposit + 1;
+           epochs[requestId].totalSupplySnapshotForDeposit;

        return assets.mulDiv(totalSupply, totalAssets, rounding);
    }
```

```diff
    function _convertToAssets(
        uint256 shares,
        uint256 requestId,
        Math.Rounding rounding
    )
        internal
        view
        returns (uint256)
    {
        if (isCurrentEpoch(requestId)) {
            return 0;
        }
-       uint256 totalAssets = epochs[requestId].totalAssetsSnapshotForRedeem + 1;
+       uint256 totalAssets = epochs[requestId].totalAssetsSnapshotForRedeem;
-       uint256 totalSupply = epochs[requestId].totalSupplySnapshotForRedeem + 1;
+       uint256 totalSupply = epochs[requestId].totalSupplySnapshotForRedeem;

        return shares.mulDiv(totalAssets, totalSupply, rounding);
    }
```
