Steep Carbon Urchin

high

# `AsyncSynthVault._claimRedeem()` should check that `lastRedeemRequestId[owner] < epochId`.

## Summary

If a user calls `requestRedeem()` and calls `claimRedeem()` in the same epoch, his withdrawn assets will be frozen in `claimableSilo` and he can never take them back. 

## Vulnerability Detail

Suppose that a user calls `requestRedeem()` and calls `claimRedeem()` in the same epoch. Then the following `_claimRedeem()` is called and `previewClaimRedeem(owner)` returns 0 in [L766]. 

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L758-L773

```javascript
    function _claimRedeem(
        address owner,
        address receiver
    )
        internal
        whenNotPaused
        returns (uint256 assets)
    {
766     assets = previewClaimRedeem(owner);
        uint256 lastRequestId = lastRedeemRequestId[owner];
        uint256 shares = epochs[lastRequestId].redeemRequestBalance[owner];
769     epochs[lastRequestId].redeemRequestBalance[owner] = 0;
        _asset.safeTransferFrom(address(claimableSilo), address(this), assets);
        _asset.transfer(receiver, assets);
        emit ClaimRedeem(lastRequestId, owner, receiver, assets, shares);
    }
```

`requestId` in [L904] is the same as the current `epochId`, because the user calls `requestRedeem()` and `claimRedeem()` in the same epoch. As a result, `_convertToAssets()` return 0 in [L905] and `previewClaimRedeem()` also returns 0.   

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L577-L581
https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L895-L911

```javascript

    function previewClaimRedeem(address owner) public view returns (uint256) {
578     uint256 lastRequestId = lastRedeemRequestId[owner];
        uint256 shares = epochs[lastRequestId].redeemRequestBalance[owner];
        return _convertToAssets(shares, lastRequestId, Math.Rounding.Floor);
    }

    function _convertToAssets(
        uint256 shares,
        uint256 requestId,
        Math.Rounding rounding
    )
        internal
        view
        returns (uint256)
    {
904     if (isCurrentEpoch(requestId)) {
905         return 0;
        }
        [...]
    }
```
This leads to `epochs[lastRequestId].redeemRequestBalance[owner] = 0` in [766] and `_asset.transfer(receiver, 0)` in [769]. Thus, all withdrawn assets would be frozen in `claimableSilo` and the user can never take them back.

## Impact

If a user calls `requestRedeem()` and calls `claimRedeem()` in the same epoch, his withdrawn assets will be frozen in `claimableSilo` and he can never take them back. 

## Code Snippet

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L758-L773

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L577-L581

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L895-L911

## Tool used

Manual Review

## Recommendation

`AsyncSynthVault._claimRedeem()` must check that `lastRedeemRequestId[owner] < epochId`.

```diff
    function _claimRedeem(
        address owner,
        address receiver
    )
        internal
        whenNotPaused
        returns (uint256 assets)
    {
+       if(lastRedeemRequestId[owner] == epochId) revert();
766     assets = previewClaimRedeem(owner);
        [...]
    }
```