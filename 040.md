Ripe Fiery Snail

medium

# Protocol fees should be transfer in the `_settle` function

## Summary

Protocol fees should be transferred in the [_settle](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L775) function

## Vulnerability Detail

Protocol fees aren't currently transferred when we call `_settle`. I've spoken with the team and they initially planned to call `settle`/`open` function with newSavedBalance/assetReturned which would be reduced by a fee that would be simulated before calling `settle`/`open`. There is a problem with this logic because if we decrease the fee before we call settle then we change the settle function, so we change the fee, and so on. In other words, there is no way to precisely decrease the fee from settle/open before because those functions depend on the fee.

## Impact

Imprecise fee taken by the protocol

## Code Snippet

## Tool used

Manual Review

## Recommendation

The solution is to transfer the fee in the `_settle` function so there won't be a need to calc it upfront
```diff
function _settle(uint256 newSavedBalance)
        internal
        onlyOwner
        whenNotPaused
        whenClosed
        returns (uint256, uint256)
    {
        (
            uint256 assetsToOwner,
            uint256 assetsToVault,
            SettleValues memory settleValues
        ) = previewSettle(newSavedBalance);

        emit EpochEnd(
            block.timestamp,
            lastSavedBalance,
            newSavedBalance,
            settleValues.fees,
            totalSupply()
        );

        // Settle the shares balance
        _burn(address(pendingSilo), settleValues.pendingRedeem);
        _mint(address(claimableSilo), settleValues.sharesToMint);

        ///////////////////////////
        // Settle assets balance //
        ///////////////////////////
        // either there are more deposits than withdrawals
        if (settleValues.pendingDeposit > settleValues.assetsToWithdraw) {
            _asset.safeTransferFrom(
                address(pendingSilo), owner(), assetsToOwner
            );
            if (settleValues.assetsToWithdraw > 0) {
                _asset.safeTransferFrom(
                    address(pendingSilo),
                    address(claimableSilo),
                    settleValues.assetsToWithdraw
                );
            }
        } else if (settleValues.pendingDeposit < settleValues.assetsToWithdraw)
        {
            _asset.safeTransferFrom(
                owner(), address(claimableSilo), assetsToVault
            );
            if (settleValues.pendingDeposit > 0) {
                _asset.safeTransferFrom(
                    address(pendingSilo),
                    address(claimableSilo),
                    settleValues.pendingDeposit
                );
            }
        } else if (settleValues.pendingDeposit > 0) {
            // if _pendingDeposit == assetsToWithdraw AND _pendingDeposit > 0
            // (and assetsToWithdraw > 0)
            _asset.safeTransferFrom(
                address(pendingSilo),
                address(claimableSilo),
                settleValues.assetsToWithdraw
            );
        }

        emit Deposit(
            address(pendingSilo),
            address(claimableSilo),
            settleValues.pendingDeposit,
            settleValues.sharesToMint
        );

        emit Withdraw(
            address(pendingSilo),
            address(claimableSilo),
            address(pendingSilo),
            settleValues.assetsToWithdraw,
            settleValues.pendingRedeem
        );

        settleValues.lastSavedBalance = settleValues.lastSavedBalance
            - settleValues.fees + settleValues.pendingDeposit
            - settleValues.assetsToWithdraw;
        lastSavedBalance = settleValues.lastSavedBalance;

        epochs[epochId].totalSupplySnapshotForDeposit =
            settleValues.totalSupplySnapshotForDeposit;
        epochs[epochId].totalAssetsSnapshotForDeposit =
            settleValues.totalAssetsSnapshotForDeposit;
        epochs[epochId].totalSupplySnapshotForRedeem =
            settleValues.totalSupplySnapshotForRedeem;
        epochs[epochId].totalAssetsSnapshotForRedeem =
            settleValues.totalAssetsSnapshotForRedeem;

        epochId++;
+       _asset.safeTransfer(owner(), settleValues.fees);       

        return (settleValues.lastSavedBalance, totalSupply());
    }
 ```
