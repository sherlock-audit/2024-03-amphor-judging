Glorious Purple Ape

high

# A very low `totalSupply` following a settlement could lead to inflation.

## Summary

When the vault is closed, if no deposit requests are made and nearly all users who hold shares request redemption for almost all of their shares, the `totalSupply` could become extremely small, potentially reaching 0 after settlement. This situation could lead to unintended inflation during the next settlement.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L775-L869

```javascript
    function _settle(uint256 newSavedBalance)
        internal
        onlyOwner
        whenNotPaused
        whenClosed
        returns (uint256, uint256)
    {
        (
            uint256 assetsToOwner,
            uint256 assetsToVault,
            SettleValues memory settleValues
        ) = previewSettle(newSavedBalance);

        emit EpochEnd(
            block.timestamp,
            lastSavedBalance,
            newSavedBalance,
            settleValues.fees,
            totalSupply()
        );

        // Settle the shares balance
797     _burn(address(pendingSilo), settleValues.pendingRedeem);
        _mint(address(claimableSilo), settleValues.sharesToMint);

        ///////////////////////////
        // Settle assets balance //
        ///////////////////////////
        // either there are more deposits than withdrawals
        if (settleValues.pendingDeposit > settleValues.assetsToWithdraw) {
            _asset.safeTransferFrom(
                address(pendingSilo), owner(), assetsToOwner
            );
            if (settleValues.assetsToWithdraw > 0) {
                _asset.safeTransferFrom(
                    address(pendingSilo),
                    address(claimableSilo),
                    settleValues.assetsToWithdraw
                );
            }
        } else if (settleValues.pendingDeposit < settleValues.assetsToWithdraw)
        {
            _asset.safeTransferFrom(
                owner(), address(claimableSilo), assetsToVault
            );
            if (settleValues.pendingDeposit > 0) {
                _asset.safeTransferFrom(
                    address(pendingSilo),
                    address(claimableSilo),
                    settleValues.pendingDeposit
                );
            }
        } else if (settleValues.pendingDeposit > 0) {
            // if _pendingDeposit == assetsToWithdraw AND _pendingDeposit > 0
            // (and assetsToWithdraw > 0)
            _asset.safeTransferFrom(
                address(pendingSilo),
                address(claimableSilo),
                settleValues.assetsToWithdraw
            );
        }

        emit Deposit(
            address(pendingSilo),
            address(claimableSilo),
            settleValues.pendingDeposit,
            settleValues.sharesToMint
        );

        emit Withdraw(
            address(pendingSilo),
            address(claimableSilo),
            address(pendingSilo),
            settleValues.assetsToWithdraw,
            settleValues.pendingRedeem
        );

        settleValues.lastSavedBalance = settleValues.lastSavedBalance
            - settleValues.fees + settleValues.pendingDeposit
            - settleValues.assetsToWithdraw;
        lastSavedBalance = settleValues.lastSavedBalance;

        epochs[epochId].totalSupplySnapshotForDeposit =
            settleValues.totalSupplySnapshotForDeposit;
        epochs[epochId].totalAssetsSnapshotForDeposit =
            settleValues.totalAssetsSnapshotForDeposit;
        epochs[epochId].totalSupplySnapshotForRedeem =
            settleValues.totalSupplySnapshotForRedeem;
        epochs[epochId].totalAssetsSnapshotForRedeem =
            settleValues.totalAssetsSnapshotForRedeem;

        epochId++;

        return (settleValues.lastSavedBalance, totalSupply());
    }
```

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L626-L684

```javascript
    function previewSettle(uint256 newSavedBalance)
        public
        view
        returns (
            uint256 assetsToOwner,
            uint256 assetsToVault,
            SettleValues memory settleValues
        )
    {
        uint256 _lastSavedBalance = lastSavedBalance;
        _checkMaxDrawdown(_lastSavedBalance, newSavedBalance);

        // calculate the fees between lastSavedBalance and newSavedBalance
        uint256 fees = _computeFees(_lastSavedBalance, newSavedBalance);
        uint256 totalSupply = totalSupply();

        // taking fees if positive yield
        _lastSavedBalance = newSavedBalance - fees;

        address pendingSiloAddr = address(pendingSilo);
        uint256 pendingRedeem = balanceOf(pendingSiloAddr);
        uint256 pendingDeposit = _asset.balanceOf(pendingSiloAddr);

649     uint256 sharesToMint = pendingDeposit.mulDiv(
            totalSupply + 1, _lastSavedBalance + 1, Math.Rounding.Floor
        );

        uint256 totalAssetsSnapshotForDeposit = _lastSavedBalance + 1;
        uint256 totalSupplySnapshotForDeposit = totalSupply + 1;

        uint256 assetsToWithdraw = pendingRedeem.mulDiv(
            _lastSavedBalance + pendingDeposit + 1,
            totalSupply + sharesToMint + 1,
            Math.Rounding.Floor
        );

        uint256 totalAssetsSnapshotForRedeem =
            _lastSavedBalance + pendingDeposit + 1;
        uint256 totalSupplySnapshotForRedeem = totalSupply + sharesToMint + 1;

        settleValues = SettleValues({
            lastSavedBalance: _lastSavedBalance + fees,
            fees: fees,
            pendingRedeem: pendingRedeem,
            sharesToMint: sharesToMint,
            pendingDeposit: pendingDeposit,
            assetsToWithdraw: assetsToWithdraw,
            totalAssetsSnapshotForDeposit: totalAssetsSnapshotForDeposit,
            totalSupplySnapshotForDeposit: totalSupplySnapshotForDeposit,
            totalAssetsSnapshotForRedeem: totalAssetsSnapshotForRedeem,
            totalSupplySnapshotForRedeem: totalSupplySnapshotForRedeem
        });

        if (pendingDeposit > assetsToWithdraw) {
            assetsToOwner = pendingDeposit - assetsToWithdraw;
        } else if (pendingDeposit < assetsToWithdraw) {
            assetsToVault = assetsToWithdraw - pendingDeposit;
        }
    }
```

In `AsyncSynthVault._settle()`, when `pendingRedeem` is very close to or equal to `totalSupply` at line 797, `totalSupply` may become extremely small, even reaching 0. Consequently, during the next settlement, the exchange rate increases at line 649 in `AsyncSynthVault.previewSettle()`.

## Impact

If `totalSupply` reaches 0 or becomes too small, unintended inflation may occur during the next settlement, resulting in users who have requested deposits receiving no shares.

## Code Snippet

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L775-L869

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L626-L684

## Tool used

Manual Review

## Recommendation

I am not sure, but I think a minimum threshold for `totalSupply` should be established.