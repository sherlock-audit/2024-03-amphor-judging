Icy Graphite Dachshund

high

# Attackers can make any user who just made a depositRequest to lose all their depositRequestBalance if called on the current epoch due to `previewClaimDeposit` returning 0.

## Summary
Attackers can make any user who just made a depositRequest to lose all their depositRequestBalance if called on the current epoch due to `previewClaimDeposit` returning 0.

## Vulnerability Detail
The claimAndRequestDeposit() function allows for anyone to deposit for any specified receiver address, however it's required users must claim their request before making a new one. So it calls `_claimDeposit()` first with the specified receiver address meaning anyone can cause any user to claim their depositRequest, that's one issue. Now, In the _claimDeposit() function:
```solidity
function _claimDeposit(
        address owner,
        address receiver
    )
        internal
        returns (uint256 shares)
    {
@>      shares = previewClaimDeposit(owner);

        uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
@>      epochs[lastRequestId].depositRequestBalance[owner] = 0;
        _update(address(claimableSilo), receiver, shares);
        emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
    }
```
Notice that it clears out the receiver's depositRequestBalance for that epoch and after that updates receivers share with the shares returned from `previewClaimDeposit()`. The issue here is `previewClaimDeposit()` will return `0` if the epoch it was called is the current epoch, regardless of this it would still clear out the receiver's depositRequestBalance which the receiver already transferred to the contract, causing clearing of the user's fund without receiving any shares. Let's see how `previewClaimDeposit()` will return `0` when the epoch is called in the current epoch. `previewClaimDeposit() simply returns the returned value from  `_convertToShares`:
```solidity
function previewClaimDeposit(address owner) public view returns (uint256) {
        uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
@>      return _convertToShares(assets, lastRequestId, Math.Rounding.Floor);
    }
```
In `_convertToShares`, it checks if the users lastDepositRequestId is the currentEpoch and returns 0 if so:
```solidity
function _convertToShares(
        uint256 assets,
        uint256 requestId,
        Math.Rounding rounding
    )
        internal
        view
        returns (uint256)
    {
        if (isCurrentEpoch(requestId)) {
 @>         return 0;
        }
        uint256 totalAssets =
            epochs[requestId].totalAssetsSnapshotForDeposit + 1;
        uint256 totalSupply =
            epochs[requestId].totalSupplySnapshotForDeposit + 1;

        return assets.mulDiv(totalSupply, totalAssets, rounding);
    }
```
This allows attackers to make any user who just made a depositRequest to lose all their depositRequestBalance by calling claimAndRequestDeposit() with an insignificant amount on behalf of the user right after the user's depositRequest.

## Impact
Attackers can make any user who just made a depositRequest to lose all their depositRequestBalance

## Code Snippet
https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L749
https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L753

## Tool used

Manual Review

## Recommendation
Consider performing check on the value returned from the `previewClaimDeposit()` function and deciding the claiming logic based on that.