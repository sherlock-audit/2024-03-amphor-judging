Glorious Purple Ape

medium

# Improper allowance checking in `VaultZapper._transferTokenInAndApprove()`.

## Summary

In `VaultZapper._transferTokenInAndApprove()`, the allowance checking should be between `VaultZapper` and `router`; however, there is an unreasonable allowance checking between `_msgSender()` and `router`.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/VaultZapper.sol#L127-L158

```javascript
    function _zapIn(
        IERC20 tokenIn,
        address router,
        uint256 amount,
        bytes calldata data
    )
        internal
    {
        uint256 expectedBalance; // of tokenIn (currently)

        if (msg.value == 0) {
            expectedBalance = tokenIn.balanceOf(address(this));
139         _transferTokenInAndApprove(router, tokenIn, amount);
        } else {
            expectedBalance = address(this).balance - msg.value;
        }

144     _executeZap(router, data); // zap

        uint256 balanceAfterZap = msg.value == 0
            ? tokenIn.balanceOf(address(this))
            : address(this).balance;

        if (balanceAfterZap > expectedBalance) {
            // Our balance is higher than expected, we shouldn't have received
            // any token
            revert InconsistantSwapData({
                expectedTokenInBalance: expectedBalance,
                actualTokenInBalance: balanceAfterZap
            });
        }
    }
```

At L139 of `VaultZapper._zapIn()`, the function `_transferTokenInAndApprove()` is called.

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/VaultZapper.sol#L160-L171

```javascript
    function _transferTokenInAndApprove(
        address router,
        IERC20 tokenIn,
        uint256 amount
    )
        internal
    {
        tokenIn.safeTransferFrom(_msgSender(), address(this), amount);
168     if (tokenIn.allowance(_msgSender(), router) < amount) {
            tokenIn.forceApprove(router, amount);
        }
    }
```

In reality, the purpose of calling `_transferTokenInAndApprove()` is to trigger `_executeZap()` at line 144 in `VaultZapper._zapIn()`.
Within `_transferTokenInAndApprove()`, if `VaultZapper` has not already approved it, approval to `router` should be granted.
However, at line 168 of `_transferTokenInAndApprove()`, there is an allowance check from `msg.sender` to `router`, rather than from `VaultZapper` to `router`.

In most scenarios, `msg.sender` does not approve any amount of `tokenIn` for the `router`. As a result, executing `VaultZapper._zapIn()` in this situation is highly likely to result in a revert.

Even if `msg.sender` has previously approved a sufficient amount of `tokenIn` for the router for other reasons, the approved amount from `VaultZapper` to the router could be reset to 0. This could be seen as unfair if `VaultZapper` had previously approved `type(uint256).max` for the `router`.

## Impact

In many instances, `VaultZapper._zapIn()` is highly likely to result in a revert. Even if it does not revert, the approved amount from `VaultZapper` to the `router` could be changed from `type(uint256).max` to 0 without the owner's involvement.

## Code Snippet

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/VaultZapper.sol#L127-L158

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/VaultZapper.sol#L160-L171

## Tool used

Manual Review

## Recommendation

`VaultZapper._transferTokenInAndApprove()` should be fixed as follows.

```diff
    function _transferTokenInAndApprove(
        address router,
        IERC20 tokenIn,
        uint256 amount
    )
        internal
    {
        tokenIn.safeTransferFrom(_msgSender(), address(this), amount);
-       if (tokenIn.allowance(_msgSender(), router) < amount) {
+       if (tokenIn.allowance(address(this), router) < amount) {
            tokenIn.forceApprove(router, amount);
        }
    }
```