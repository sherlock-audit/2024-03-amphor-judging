Skinny Leather Mantis

medium

# Invalid allowance check in `VaultZapper._transferTokenInAndApprove`

## Summary
The `VaultZapper` smart contract is used to exchange `tokenIn` for an asset token used in `SynthVault` via the 1inch router. Before interacting with the 1inch router smart contract, it is necessary to provide it with an allowance, but this may not occur due to an incorrect allowance check.

## Vulnerability Detail
Suppose a user wants to exchange DAI for USDT and deposit those USDT into `SyncSynthVault`. 
The user provides an allowance to the `VaultZapper` smart contract to spend DAI and call the `VaultZapper.zapAndDeposit` function:
```solidity
function zapAndDeposit(
        IERC20 tokenIn,
        IERC4626 vault,
        address router,
        uint256 amount,
        bytes calldata data
    )
        public
        payable
        onlyAllowedRouter(router)
        onlyAllowedVault(vault)
        whenNotPaused
        returns (uint256)
    {
        ///...
        // Zap
        _zapIn(tokenIn, router, amount, data);
        ///...
}
```

Inside the `_zapIn` function, the `_transferTokenInAndApprove` function is called, which transfers DAI tokens from the user to the `VaultZapper` smart contract and provides an allowance for the router to take these DAI tokens for swapping with the `VaultZapper` smart contract.
```solidity
if (msg.value == 0) {
            expectedBalance = tokenIn.balanceOf(address(this));
-->         _transferTokenInAndApprove(router, tokenIn, amount);
        } else {
        
 function _transferTokenInAndApprove(
        address router,
        IERC20 tokenIn,
        uint256 amount
    )
        internal
    {
        tokenIn.safeTransferFrom(_msgSender(), address(this), amount);
-->     if (tokenIn.allowance(_msgSender(), router) < amount) {
            tokenIn.forceApprove(router, amount);
        }
    }        
```
The issue lies in the fact that the allowance is checked for `msg.sender` instead of `address(this)`.

A small test case:

1) The user calls `dai.approve(1inch_router, 100e18)`;
2) After some time, the user calls `zapAndDeposit` `DAI -> USDT`, amount - `50e18`.
3) Inside `_transferTokenInAndApprove`, the if statement fails because `tokenIn.allowance(_msgSender(), router) (100e18) < amount (50e18)`. This causes the transaction to fail in the 1inch router smart contract due to insufficient allowance.

`Note`: `approveTokenForRouter` can prevent this issue, but in that case, the owner would need to call this function for all ERC20 tokens and whitelisted routers, which is not feasible.

## Impact
Users who have provided an allowance to the 1inch router for the token that will be used with `VaultZapper` will not be able to execute the transaction successfully.

## Code Snippet
[src/VaultZapper.sol#L168](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/VaultZapper.sol#L168)

## Tool used

Manual Review

## Recommendation
Consider adding a small change that prevents this kind of issue:
```diff
function _transferTokenInAndApprove(
        address router,
        IERC20 tokenIn,
        uint256 amount
    )
        internal
    {
        tokenIn.safeTransferFrom(_msgSender(), address(this), amount);
-        if (tokenIn.allowance(_msgSender(), router) < amount) {
+        if (tokenIn.allowance(address(this), router) < amount) {
            tokenIn.forceApprove(router, amount);
        }
    }
```
