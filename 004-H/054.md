Precise Sandstone Fox

high

# Wrong receiver in `AsyncSynthVault::_createRedeemRequest`, which makes impossible for a receiver to claim a redeem

## Summary
`AsyncSynthVault::requestRedeem` function gives possibillity to create requests by the owner for another address - receiver. When doing so, `AsyncSynthVault::_createRedeemRequest` should add shares to the receiver's `redeemRequestBalance` of the `epochs` mapping to the current epoch, and add current epoche to the receiver `lastRedeemRequestId`, but it does not.

## Vulnerability Detail
When an owner creates redeem request for a receiver, `AsyncSynthVault::_createRedeemRequest` should add shares to the receiver's `redeemRequestBalance` of the `epochs` mapping to the current epoch id, and add current epoche id to the receiver's `lastRedeemRequestId`.  Instead of adding current epoche id to the recevier's `lastRedeemRequestId`, the contract adds the epoche id to the owner's `lastRedeemRequestId`. This makes impossible for a receiver to claim his redeem after the vault is open , as the receiver's `lastRedeemRequestId` would equal to 0, and  thus receiver's `epochs[0].redeemRequestBalance[receiver]` would also equal to zero. So even though the receiver has a right to claim a redeem, he/she cannot do it.

## Impact
Receiver cannot redeem assets from the contract, as well as the owner who created the redeem request

## Code Snippet
https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L730
`lastRedeemRequestId[owner] = epochId;`

##Proof of Concept
The test shows that a receiver (USER2) does not receive any redeem request balance, even though owner(USER) created redeem request for the receiver.

<details>
  <summary>Test</summary>

```solidity
//SPDX-License-Identifier: MIT
pragma solidity 0.8.21;

import {Test, console} from "forge-std/Test.sol";
import {ERC20} from "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import {AsyncSynthVault} from "../src/AsyncSynthVault.sol";

contract MeTest is Test {
    ERC20Token token;
    AsyncSynthVault vault;

    address USER = makeAddr("user");
    address USER2 = makeAddr("user2");
    address USER3 = makeAddr("user3");

    function setUp() public {
        vm.deal(USER, 10 ether);
        vm.deal(USER2, 10 ether);
        vm.deal(USER3, 10 ether);
        token = new ERC20Token("", "");
        token.mint(USER, 10 ether);
        token.mint(USER2, 10 ether);
        token.mint(USER3, 10 ether);
        vm.prank(USER);
        vault = new AsyncSynthVault();
        vault.initialize(300, USER, token, "", "");
    }

    function testRedeemRequest() public {
        vm.startPrank(USER);
        token.approve(address(vault), 1 ether);
        uint shares = vault.deposit(1 ether, USER);

        vault.close();

        vault.requestRedeem(shares, USER2, USER, "");

        uint lastRedeemRequestIdUser2 = vault.lastRedeemRequestId(USER2);
        uint lastRedeemRequestIdUser = vault.lastRedeemRequestId(USER);

        // USER2 was redeem requested by USER, so USER should have 1 if, and USER 0 id, but it is save in reverse
        assertEq(lastRedeemRequestIdUser2, 1);
        assertEq(lastRedeemRequestIdUser, 0);
        token.approve(address(vault), 6 ether);
        vault.open(5 ether);

        vm.stopPrank();

        vm.startPrank(USER2);
        uint balanceBeforeClaim = token.balanceOf(USER2);
        vault.claimRedeem(USER2);
        uint balanceAfterClaim = token.balanceOf(USER2);

        // after claiming USER2 should receive tokens he was requested by USER, but he didn't
        assert(balanceAfterClaim > balanceBeforeClaim);
        vm.stopPrank();
    }
}

contract ERC20Token is ERC20 {
    constructor(string memory name, string memory symbol) ERC20(name, symbol) {}

    function mint(address receiver, uint value) public {
        _mint(receiver, value);
    }
}
```

</details>

## Tool used

Manual Review

## Recommendation
Change `lastRedeemRequestId[owner] = epochId;` to `lastRedeemRequestId[receiver] = epochId;`.

```diff
    function _createRedeemRequest(
        uint256 shares,
        address receiver,
        address owner,
        bytes memory data
    )
        internal
    {
        epochs[epochId].redeemRequestBalance[receiver] += shares;
-        lastRedeemRequestId[owner] = epochId;
+        lastRedeemRequestId[receiver] = epochId;
...
```
