Tame Steel Jellyfish

high

# User's requestDeposit funds may be lost via claimAndRequestDeposit()

## Summary
Users current epoch's requestDeposit funds may be reseted if we claim deposit.

## Vulnerability Detail
Users can claim their deposits belonging to previous epochs. Users are not allowed to claim their deposits belonging to current epoch. If users deposit request in current epoch, and claim deposit, no shares will be minted and existing depositRequestBalance will be reset to 0, which means users' deposit request will be reset and their funds for deposit request will be lost. Especially, another people can help claim people's deposit via claimAndRequestDeposit(), this is very dangerous.

```solidity
    function _claimDeposit(
        address owner,
        address receiver
    )
        internal
        returns (uint256 shares)
    {
        shares = previewClaimDeposit(owner);

        uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
        epochs[lastRequestId].depositRequestBalance[owner] = 0;
        _update(address(claimableSilo), receiver, shares);
        emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
    }

    function previewClaimDeposit(address owner) public view returns (uint256) {
        uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
        return _convertToShares(assets, lastRequestId, Math.Rounding.Floor);
    }

    function _convertToShares(
        uint256 assets,
        uint256 requestId,
        Math.Rounding rounding
    )
        internal
        view
        returns (uint256)
    {
        if (isCurrentEpoch(requestId)) {
            return 0;
        }
        uint256 totalAssets =
            epochs[requestId].totalAssetsSnapshotForDeposit + 1;
        uint256 totalSupply =
            epochs[requestId].totalSupplySnapshotForDeposit + 1;

        return assets.mulDiv(totalSupply, totalAssets, rounding);
    }
``` 
Eg.
- Vault is closed.
- Alice requests deposit 1000 Tokens via requestDeposit()
- Bob claim Alice's deposit and help Alice request deposit 0 Token via claimAndRequestDeposit()
- Alice's pending deposit request is 0. And she can claim nothing once epoch passes.

POC:
```solidity
    function test_myclaim() external {
        usersDealApproveAndDeposit(vaultTested, 2);
        assertClose(vaultTested);
        //User 1, deposit request 1000
        vm.startPrank(user1.addr);
        vaultTested.requestDeposit(1000, user1.addr, user1.addr, "");
        vm.stopPrank();
        // User 2 claim user1's deposit requets.
        vm.startPrank(user2.addr);
        console.log(vaultTested.pendingDepositRequest(user1.addr));
        vaultTested.claimAndRequestDeposit(0, user1.addr, "");
        console.log(vaultTested.pendingDepositRequest(user1.addr));
        require(vaultTested.pendingDepositRequest(user1.addr) == 0);
        vm.stopPrank();
    }
```
## Impact
Users will lose their deposit request funds.

## Code Snippet
https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L742-L756
https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L567-L571

## Tool used

Manual Review

## Recommendation
If users claim current epoch's deposit, don't reset depositRequestBalance.
```diff
diff --git a/asynchronous-vault/src/AsyncSynthVault.sol b/asynchronous-vault/src/AsyncSynthVault.sol
index 6d2a9f3..6a305d0 100644
--- a/asynchronous-vault/src/AsyncSynthVault.sol
+++ b/asynchronous-vault/src/AsyncSynthVault.sol
@@ -750,7 +750,9 @@ contract AsyncSynthVault is IERC7540, SyncSynthVault {
 
         uint256 lastRequestId = lastDepositRequestId[owner];
         uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
-        epochs[lastRequestId].depositRequestBalance[owner] = 0;
+        if (epochId != lastRequestId) {
+            epochs[lastRequestId].depositRequestBalance[owner] = 0;
+        }
         _update(address(claimableSilo), receiver, shares);
         emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
     }
``` 