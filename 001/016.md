Striped Raspberry Bird

high

# `claimAndRequestDeposit` can be used to make any user lose their deposit requests of the current epoch

## Summary

## Vulnerability Detail
AsyncVault extends the SyncVault functionality with requests for deposit and withdrawal. These requests are implemented the following way:

1. User requests the deposit (redeem). Tokens (shares) are transferred from the user to `pendingSilo`.

2. The epoch is settled.

3. User is able to `claimDeposit` (`claimRedeem`), which transfers shares (tokens) from `claimableSilo` to the user.

Before step 2, functions `claimDeposit` / `claimRedeem` do not revert if the request's epoch is the same as the current epoch, but instead they wipe user's pending balance and send the user nothing in return, because `previewClaimDeposit` / `previewClaimRedeem` return zero if the epoch is the current one. Thus, if the user claims his request before the epoch is settled, they will permanently lose the requested deposit/redeem.

Moreover, because `claimAndRequestDeposit` does not have access controls and allows the caller to specify both parameters for `claimDeposit`, anybody can wipe anyone's deposit request created in the current epoch.

### Proof of Concept
 
1. Alice calls `requestDeposit`. Her 1000 USDC are sent to `pendingSilo`. Her request is stored in the `depositRequestBalance` mapping.
2. Malicious Bob calls `claimAndRequestDeposit` with 1 wei, specifying Alice as the `receiver`:
```solidity
    function claimAndRequestDeposit(
        uint256 assets,
        address receiver,
        bytes memory data
    )
        external
    {
        _claimDeposit(receiver, receiver); // (alice, alice)
        requestDeposit(assets, receiver, _msgSender(), data);
    }
```
```solidity
    function _claimDeposit(
        address owner, // alice
        address receiver // alice
    )
        internal
        returns (uint256 shares)
    {
        shares = previewClaimDeposit(owner); // zero
        uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
        epochs[lastRequestId].depositRequestBalance[owner] = 0; // alice's value is set to zero
        _update(address(claimableSilo), receiver, shares); // alice receives zero shares
        emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
    }
```
```solidity
    function previewClaimDeposit(address owner) public view returns (uint256) {
        uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
        return _convertToShares(assets, lastRequestId, Math.Rounding.Floor);
    }
```
```solidity
    function _convertToShares(
        uint256 assets,
        uint256 requestId,
        Math.Rounding rounding
    )
        internal
        view
        returns (uint256)
    {
        if (isCurrentEpoch(requestId)) {
            return 0;
        }
        /*...*/
    }    
```
This results in Alice's request being deleted, so Alice will never be able to claim her deposit request, nor to return her 1000 USDC.

## Impact
1. Anyone can trigger permanent loss of funds for anyone who requestedDeposit during the current epoch.
2. Users lose their assets (shares) used in their deposit (redeem) request if they call `claimDeposit` (`claimRedeem`) in the same epoch the request was made.
## Code Snippet
https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L504-L527
## Tool used

Manual Review

## Recommendation
1. `claimAndRequestDeposit` should use `_msgSender` for `_claimDeposit` instead of allowing the caller to specify it.
```diff
    function claimAndRequestDeposit(
        uint256 assets,
-       address receiver,
        bytes memory data
    )
        external
    {
+       address owner = _msgSender();
+       _claimDeposit(owner, owner);
-       _claimDeposit(receiver, receiver);
        requestDeposit(assets, receiver, _msgSender(), data);
    }
```

2. `claimDeposit` and `claimRedeem` should revert if the request's epoch has not finished.