Glorious Purple Ape

medium

# Performing `claimRedeem()` before settlement results in frozen assets.

## Summary

When a user invokes `AsyncSynthVault.claimRedeem()` before settlement, no assets are transferred, and the `redeemRequestBalance` of the user is decreased to 0.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L521-L527

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L577-L581

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L758-L773

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L895-L911

```javascript
    function claimRedeem(address receiver)
        public
        whenNotPaused
        returns (uint256 assets)
    {
        return _claimRedeem(_msgSender(), receiver);
    }

    function previewClaimRedeem(address owner) public view returns (uint256) {
578     uint256 lastRequestId = lastRedeemRequestId[owner];
        uint256 shares = epochs[lastRequestId].redeemRequestBalance[owner];
        return _convertToAssets(shares, lastRequestId, Math.Rounding.Floor);
    }

    function _claimRedeem(
        address owner,
        address receiver
    )
        internal
        whenNotPaused
        returns (uint256 assets)
    {
766     assets = previewClaimRedeem(owner);
        uint256 lastRequestId = lastRedeemRequestId[owner];
        uint256 shares = epochs[lastRequestId].redeemRequestBalance[owner];
        epochs[lastRequestId].redeemRequestBalance[owner] = 0;
        _asset.safeTransferFrom(address(claimableSilo), address(this), assets);
        _asset.transfer(receiver, assets);
        emit ClaimRedeem(lastRequestId, owner, receiver, assets, shares);
    }

    function _convertToAssets(
        uint256 shares,
        uint256 requestId,
        Math.Rounding rounding
    )
        internal
        view
        returns (uint256)
    {
        if (isCurrentEpoch(requestId)) {
            return 0;
        }
        uint256 totalAssets = epochs[requestId].totalAssetsSnapshotForRedeem + 1;
        uint256 totalSupply = epochs[requestId].totalSupplySnapshotForRedeem + 1;

        return shares.mulDiv(totalAssets, totalSupply, rounding);
    }
```

Let's assume that a user has already requested a certain amount of redemption during the current `epochId`.
The function `AsyncSynthVault._convertToAssets()` consistently returns 0 for the current `epochId`.
Therefore, prior to settlement, `AsyncSynthVault.previewClaimRedeem()` will return 0 because the `lastRequestId` at L578 corresponds to the current `epochId`.
Consequently, the value of `assets` at L766 in `AsyncSynthVault._claimRedeem()` is 0, indicating that no assets are being transferred.
This implies that the assets associated with the `redeemRequestBalance` of the user's shares will be locked in the `claimableSilo` after settlement.

## Impact

Executing `claimRedeem()` before settlement results in frozen assets.

## Code Snippet

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L521-L527

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L577-L581

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L758-L773

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L895-L911

## Tool used

Manual Review

## Recommendation

`AsyncSynthVault._claimRedeem()` should be fixed as follows.

```diff
    function _claimRedeem(
        address owner,
        address receiver
    )
        internal
        whenNotPaused
        returns (uint256 assets)
    {
+       if(lastRedeemRequestId[owner] == epochId) revert NotSettled();
        assets = previewClaimRedeem(owner);
        uint256 lastRequestId = lastRedeemRequestId[owner];
        uint256 shares = epochs[lastRequestId].redeemRequestBalance[owner];
        epochs[lastRequestId].redeemRequestBalance[owner] = 0;
        _asset.safeTransferFrom(address(claimableSilo), address(this), assets);
        _asset.transfer(receiver, assets);
        emit ClaimRedeem(lastRequestId, owner, receiver, assets, shares);
    }
```