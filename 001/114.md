Nice Smoke Spider

high

# lastRedeemRequestId is wrongly updated when createRedeemRequest

## Summary
when `requestRedeem` is called to create redeem request for receiver, the `lastRedeemRequestId` is correctly updated to owner but not receiver.

## Vulnerability Detail
when `requestRedeem` is called, the latest `epochId`  should be updated to `lastRedeemRequestId[receiver]` for later reclaim. However, in the `_createRedeemRequest`, `lastRedeemRequestId[owner]` is updated, and `lastRedeemRequestId[receiver]` is still it's old value.
when owner != receiver, `lastRedeemRequestId[receiver]` could be outdated and lead to issues and there's no direct way to correct `lastRedeemRequestId`.

```solidity
    function _createRedeemRequest(
        uint256 shares,
        address receiver,
        address owner,
        bytes memory data
    )
        internal
    {
        epochs[epochId].redeemRequestBalance[receiver] += shares;
        // @audit: when owner != receiver, this is incorrect
        lastRedeemRequestId[owner] = epochId;

        if (
            data.length > 0
                && ERC7540Receiver(receiver).onERC7540RedeemReceived(
                    _msgSender(), owner, epochId, data
                ) != ERC7540Receiver.onERC7540RedeemReceived.selector
        ) revert ReceiverFailed();

        emit RedeemRequest(receiver, owner, epochId, _msgSender(), shares);
    }
```
## Impact
The incorrect `lastRedeemRequestId` will lead to
Impact 1) receiver can't claim their redeemed asset:
```solidity
    function test_Impact_1() external {
        usersDealApproveAndDeposit(vaultTested, 4);
        usersDealApprove(vaultTested, 5);
        assertClose(vaultTested);
        
        uint256 assets =
            IERC20Metadata(vaultTested.asset()).balanceOf(user5.addr);
        assertRequestDeposit(
            vaultTested, user5.addr, user5.addr, user5.addr, assets, ""
        );
        assertOpen(vaultTested, 3);
        assertClose(vaultTested);
        assertClaimDeposit(vaultTested, user5.addr, user5.addr, assets);

        uint256 beforeBalance = vaultTested.balanceOf(user9.addr);


        uint256 shares = vaultTested.balanceOf(user5.addr);
        assets =
            IERC20Metadata(vaultTested.asset()).balanceOf(user5.addr);
        
        vm.startPrank(user5.addr);
        // @audit: user5 (owner) create redeem request, user9 is receiver
        vaultTested.requestRedeem(shares, user9.addr, user5.addr, "");
        vm.stopPrank();

        assertOpen(vaultTested, 3);
        assertClose(vaultTested);

        vm.startPrank(user9.addr);
        vaultTested.claimRedeem(user9.addr);
        vm.stopPrank();

        uint256 afterBalance = vaultTested.balanceOf(user9.addr);
        // @audit: assert failed, user9 can't reclaim the asset 
        assertTrue(afterBalance > beforeBalance);
    }

```

Impact 2) multiple redeem requests could be created for receiver, bypass checker https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L490-L492
```solidity
function test_Impact_2() external {
        // it should revert with ERC4626ExceededMaxClaim
        usersDealApproveAndDeposit(vaultTested, 4);
        usersDealApprove(vaultTested, 5);
        assertClose(vaultTested);

        uint256 assets =
            IERC20Metadata(vaultTested.asset()).balanceOf(user5.addr);
        assertRequestDeposit(
            vaultTested, user5.addr, user5.addr, user5.addr, assets, ""
        );
        assertRequestDeposit(
            vaultTested, user1.addr, user1.addr, user1.addr, assets, ""
        );
        assertOpen(vaultTested, 3);
        assertClose(vaultTested);
        assertClaimDeposit(vaultTested, user5.addr, user5.addr, assets);

        assets =
            IERC20Metadata(vaultTested.asset()).balanceOf(user5.addr);
        
        // @audit: user5 (owner) create redeem request, user9 is receiver
        vm.startPrank(user5.addr);
        uint256 shares = vaultTested.balanceOf(user5.addr);
        vaultTested.requestRedeem(shares, user9.addr, user5.addr, "");
        vm.stopPrank();

        vm.startPrank(user1.addr);
        shares = vaultTested.balanceOf(user1.addr);
        // @audit: user1 (owner) create redeem request, user9 is receiver
        // @audit: this should be failed since user1 has unclaimed redeem asset, but the test passed
        vaultTested.requestRedeem(shares, user9.addr, user1.addr, "");
        vm.stopPrank();
    }
```
Impact 3)  user will lost his asset waiting to be claimed if he creates redeem request to other receiver and his `lastRedeemRequestId` is updated to latest `epochId`.
```solidity
function test_Impact_3() external {
        usersDealApproveAndDeposit(vaultTested, 4);
        usersDealApprove(vaultTested, 5);
        assertClose(vaultTested);
        
        uint256 assets =
            IERC20Metadata(vaultTested.asset()).balanceOf(user5.addr);
        assertRequestDeposit(
            vaultTested, user5.addr, user5.addr, user5.addr, assets, ""
        );
        assertOpen(vaultTested, 3);
        assertClose(vaultTested);
        assertClaimDeposit(vaultTested, user5.addr, user5.addr, assets);


        uint256 shares = vaultTested.balanceOf(user5.addr);
        assets =
            IERC20Metadata(vaultTested.asset()).balanceOf(user5.addr);
        
        vm.startPrank(user5.addr);
        // @audit: user5 (owner) create redeem request for itself (user5 is receiver), using shares / 2
        vaultTested.requestRedeem(shares/2, user5.addr, user5.addr, "");
        vm.stopPrank();

        assertOpen(vaultTested, 3);
        assertClose(vaultTested);
        
        vm.startPrank(user5.addr);
        shares = vaultTested.balanceOf(user5.addr);
        // @audit: user5 (owner) create redeem request, user9 is receiver, using leftout shares
        vaultTested.requestRedeem(shares, user9.addr, user5.addr, "");

        assertOpen(vaultTested, 3);
        assertClose(vaultTested);

        vm.startPrank(user5.addr);
        vaultTested.claimRedeem(user5.addr);
        vm.stopPrank();

        shares = vaultTested.balanceOf(user5.addr);
        assets =
            IERC20Metadata(vaultTested.asset()).balanceOf(user5.addr);
        // @audit: user5 failed to claim assets correspoding to half of shares he redeemed to himself
        assertEq(shares, 0);
        assertEq(assets, 0);
    }
```


## Code Snippet
https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L721-L740

## Tool used

Manual Review and Foundry

## Recommendation
`_createRedeemRequest` should correctly update `lastRedeemRequestId[receiver]` 
```solidity
if (lastRedeemRequestId[receiver] != epochId) {
    lastRedeemRequestId[receiver] = epochId;
}
```