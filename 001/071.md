Tart Raisin Cormorant

high

# Loss of funds when requesting a redeem for a receiver different than the owner

## Summary

The request redeem process has a fundamental flaw that causes a request to be lost when the receiver of the redemption is different from the owner of the shares.

## Vulnerability Detail

When requesting a redemption, the owner of the shares can specify an arbitrary receiver to eventually claim the request. The `_createRedeemRequest()` function executes the following:

```solidity
function _createRedeemRequest(
    uint256 shares,
    address receiver,
    address owner,
    bytes memory data
)
    internal
{
    epochs[epochId].redeemRequestBalance[receiver] += shares;
    lastRedeemRequestId[owner] = epochId;

    if (
        data.length > 0
            && ERC7540Receiver(receiver).onERC7540RedeemReceived(
                _msgSender(), owner, epochId, data
            ) != ERC7540Receiver.onERC7540RedeemReceived.selector
    ) revert ReceiverFailed();

    emit RedeemRequest(receiver, owner, epochId, _msgSender(), shares);
}
```

Note how the `redeemRequestBalance` mapping is updated for the `receiver`, **but the `lastRedeemRequestId` is updated for the `owner`**.

This causes a fatal issue in which the state gets corrupted, the `receiver` has the pending shares associated but the epoch is not updated for his account, causing `_claimRedeem()` to miss the redemption when attempting to claim.

```solidity
function _claimRedeem(
    address owner,
    address receiver
)
    internal
    whenNotPaused
    returns (uint256 assets)
{
    assets = previewClaimRedeem(owner);
    uint256 lastRequestId = lastRedeemRequestId[owner];
    uint256 shares = epochs[lastRequestId].redeemRequestBalance[owner];
    epochs[lastRequestId].redeemRequestBalance[owner] = 0;
    _asset.safeTransferFrom(address(claimableSilo), address(this), assets);
    _asset.transfer(receiver, assets);
    emit ClaimRedeem(lastRequestId, owner, receiver, assets, shares);
}

function previewClaimRedeem(address owner) public view returns (uint256) {
    uint256 lastRequestId = lastRedeemRequestId[owner];
    uint256 shares = epochs[lastRequestId].redeemRequestBalance[owner];
    return _convertToAssets(shares, lastRequestId, Math.Rounding.Floor);
}
```

The implementation will fetch the `lastRequestId` from the `lastRedeemRequestId` mapping, but since this has not been updated with the proper epoch id, it will return either zero (the default value) or a previous epoch (from an older request), causing shares to be zero in `previewClaimRedeem()` when accessing the `redeemRequestBalance` mapping.

## Proof of concept

The following proof of concept shows the issue:

```solidity
function test_RedeemForReceiverFails() public {
    asset.mint(alice, 1_000e18);
    asset.mint(owner, 1_000e18);

    vm.prank(alice);
    vault.deposit(1_000e18, alice);

    // vault is closed
    vm.prank(owner);
    vault.close();

    // alice creates a redeem request for bob
    vm.startPrank(alice);

    vault.requestRedeem(vault.balanceOf(alice), bob, alice, "");

    vm.stopPrank();

    // owner opens vault
    vm.prank(owner);
    vault.open(2_000e18);

    // bob tries to claim redeem
    vm.prank(bob);
    vault.claimRedeem(bob);

    // bob doesnt have any assets
    assertEq(0, asset.balanceOf(bob));
}
```

## Impact

Requested redemptions for a receiver different than the owner will be lost, causing loss of funds as the request cannot be claimed by the receiver.

## Code Snippet

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L729-L730

## Tool used

Manual Review

## Recommendation

Update the `lastRedeemRequestId` for the `receiver` instead of the `owner`.

```diff
    epochs[epochId].redeemRequestBalance[receiver] += shares;
-   lastRedeemRequestId[owner] = epochId;
+   lastRedeemRequestId[receiver] = epochId;
```
