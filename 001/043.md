Glorious Purple Ape

high

# A malicious user can freeze any other users' requested shares in `claimableSilo`.

## Summary

A malicious user can freeze the requested shares of other users by invoking `AsyncSynthVault.claimAndRequestDeposit()` before settlement. Therefore, other users will not receive any shares after settlement.

## Vulnerability Detail

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L204-L213

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L567-L571

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L742-L756

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L875-L893

```javascript
    function claimAndRequestDeposit(
        uint256 assets,
        address receiver,
        bytes memory data
    )
        external
    {
211     _claimDeposit(receiver, receiver);
        requestDeposit(assets, receiver, _msgSender(), data);
    }

    function previewClaimDeposit(address owner) public view returns (uint256) {
568     uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
        return _convertToShares(assets, lastRequestId, Math.Rounding.Floor);
    }

    function _claimDeposit(
        address owner,
        address receiver
    )
        internal
        returns (uint256 shares)
    {
749     shares = previewClaimDeposit(owner);

        uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
        epochs[lastRequestId].depositRequestBalance[owner] = 0;
        _update(address(claimableSilo), receiver, shares);
        emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
    }

    function _convertToShares(
        uint256 assets,
        uint256 requestId,
        Math.Rounding rounding
    )
        internal
        view
        returns (uint256)
    {
        if (isCurrentEpoch(requestId)) {
            return 0;
        }
        uint256 totalAssets =
            epochs[requestId].totalAssetsSnapshotForDeposit + 1;
        uint256 totalSupply =
            epochs[requestId].totalSupplySnapshotForDeposit + 1;

        return assets.mulDiv(totalSupply, totalAssets, rounding);
    }
```

Let's consider a scenario where Bob has requested a deposit amount during the current `epochId`. Subsequently, the malicious user triggers `AsyncSynthVault.claimAndRequestDeposit()` with Bob's address as the second parameter `receiver` and 0 as the first parameter `assets` before settlement. Referring to line 211, it invokes `_claimDeposit(Bob, Bob)`.

At line 749 of `AsyncSynthVault._claimDeposit()`, the `shares` are set to 0, resulting in no shares being allocated. This happens as follows:
In `AsyncSynthVault.previewClaimDeposit()`, the `lastRequestId` at line 568 corresponds to the current `epochId`, and `AsyncSynthVault._convertToShares()` consistently returns 0 for the current `epochId`. Therefore, `AsyncSynthVault.previewClaimDeposit()` returns 0.

As a result, the shares corresponding to the `depositRequestBalance` of Bob's underlying assets will be locked in the `claimableSilo` after settlement.

Furthermore, Bob cannot reduce his requested deposit amount as his `depositRequestBalance` has already been reset to 0.

## Impact

An attacker can freeze the requested shares of any other users without any sacrifice.

## Code Snippet

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L204-L213

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L567-L571

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L742-L756

https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L875-L893

## Tool used

Manual Review

## Recommendation

`AsyncSynthVault._claimDeposit()` should be fixed as follows.

```diff
    function _claimDeposit(
        address owner,
        address receiver
    )
        internal
        returns (uint256 shares)
    {
+       if(lastDepositRequestId[owner] == epochId) revert NotSettled();
         shares = previewClaimDeposit(owner);

        uint256 lastRequestId = lastDepositRequestId[owner];
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
        epochs[lastRequestId].depositRequestBalance[owner] = 0;
        _update(address(claimableSilo), receiver, shares);
        emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
    }
```