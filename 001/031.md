Ripe Fiery Snail

high

# Users can reset their deposit/redeem requests

## Summary

Users can accidentally reset their deposit/redeem requests, without transferring asset/shares to them, by calling [claimDeposit](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L508), or [claimRedeem](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L521) in the current epoch.

## Vulnerability Detail

Users who have some pending requests/redeem, and will call claimDeposit/claimRedeem in the same, will reset their requests, and lost funds. The root cause of this issue is that [_convertToShares](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L884), and [_convertToAssets](https://github.com/sherlock-audit/2024-03-amphor/blob/main/asynchronous-vault/src/AsyncSynthVault.sol#L895C14-L895C30) returns 0 if we are in the current epoch. Because claimDeposit/claimRedeem are possible to call in the current epoch and not revert, a user would effectively reset his pending requests, and lose funds.

## Impact

Funds lost by users forever

## Code Snippet

Reset deposit request

```solidity
 function test_reset_my_deposit_request() external {
        // vault cannot be empty to close it
        usersDealApproveAndDeposit(vaultTested, 1);
        // close the vault so we can request a deposit
        close(vaultTested);
        // user1, and user2 deposits
        usersDealApproveAndRequestDeposit(vaultTested, 2, "");

        // at this point, user2 has a non-zero deposit request
        assertGt(vaultTested.pendingDepositRequest(user2.addr), 0, "Deposit request should be non-zero");

        vm.prank(user2.addr);
        vaultTested.claimDeposit(user2.addr);
        // user2 has claimed the deposit, which reset user's deposit request
        // balance to 0, without
        // transferring shares to the user
        assertEq(vaultTested.pendingDepositRequest(user2.addr), 0, "Deposit request should be reset");
        assertEq(vaultTested.balanceOf(user2.addr), 0, "Shares should not be transferred");
    }
```

Reset redeem request

```solidity
function test_reset_my_redeem_request() external {
        // vault cannot be empty to close it
        usersDealApproveAndDeposit(vaultTested, 1);
        // close the vault so we can request a deposit
        close(vaultTested);
        // user1 request redeem
        usersDealApproveAndRequestRedeem(vaultTested, 1, "");

        // at this point, user2 has a non-zero deposit request
        assertGt(vaultTested.pendingRedeemRequest(user1.addr), 0, "Redeem request should be non-zero");

        uint256 assetBalanceBefore = IERC20(vaultTested.asset()).balanceOf(user1.addr);

        vm.prank(user1.addr);
        vaultTested.claimRedeem(user1.addr);

        // user1 has claimed the deposit, which reset user's deposit request
        // balance to 0, without
        // transferring asset to the user
        assertEq(vaultTested.pendingRedeemRequest(user1.addr), 0, "Redeem request should be reset");

       uint256 assetBalanceAfter = IERC20(vaultTested.asset()).balanceOf(user1.addr);
       assertEq(assetBalanceAfter, assetBalanceBefore, "Asset should not be transferred");
    }
```

## Tool used

Manual Review

## Recommendation

Add a check to `_claimDeposit`/`_claimRedeem`, and revert if the claim is made in the same epoch as the request

```diff
+ error UnableToClaimAtSameEpoch(uint256 lastRequestId);

...

function _claimDeposit(
        address owner,
        address receiver
    )
        internal
        returns (uint256 shares)
    {
-       shares = previewClaimDeposit(owner);

        uint256 lastRequestId = lastDepositRequestId[owner];
+       if (isCurrentEpoch(lastRequestId)) {
+          revert UnableToClaimAtSameEpoch(lastRequestId);
+       }
+       shares = previewClaimDeposit(owner);
        uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
        epochs[lastRequestId].depositRequestBalance[owner] = 0;
        _update(address(claimableSilo), receiver, shares);
        emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
    }
    
    function _claimRedeem(
        address owner,
        address receiver
    )
        internal
        whenNotPaused
        returns (uint256 assets)
    {
-       assets = previewClaimRedeem(owner);
        uint256 lastRequestId = lastRedeemRequestId[owner];
+       if (isCurrentEpoch(lastRequestId)) {
+          revert UnableToClaimAtSameEpoch(lastRequestId);
+       }
+       assets = previewClaimRedeem(owner);
        uint256 shares = epochs[lastRequestId].redeemRequestBalance[owner];
        epochs[lastRequestId].redeemRequestBalance[owner] = 0;
        _asset.safeTransferFrom(address(claimableSilo), address(this), assets);
        _asset.transfer(receiver, assets);
        emit ClaimRedeem(lastRequestId, owner, receiver, assets, shares);
    }

```
