Decent Malachite Dragon

high

# Calls to `claimDeposit` causes pending deposits to be lost.

## Summary

When attempting to [`_claimDeposit(address,address)`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L742) whilst having a [`lastRequestId`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L751) that is equal to the current [`epochId`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L107), a user's pending deposit will be exchanged for zero vault shares, resulting in a loss of funds.

## Vulnerability Detail

When calling [`_claimDeposit(address,address)`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L742), the number of corresponding [`shares`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L749) to mint are calculated based upon a call to [`previewClaimDeposit(address)`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L567):

```solidity
/**
 * @dev This function lets users preview how many shares they will get if
 * they claim their deposit request.
 */
function previewClaimDeposit(address owner) public view returns (uint256) {
    uint256 lastRequestId = lastDepositRequestId[owner];
    uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
    return _convertToShares(assets, lastRequestId, Math.Rounding.Floor);
}
```

This underlyingly depends on [`_convertToShares(uint256,uint256,uint8)`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L874C1-L893C6):

```solidity
function _convertToShares(
    uint256 assets,
    uint256 requestId,
    Math.Rounding rounding
) internal view returns (uint256) {
    if (isCurrentEpoch(requestId)) {
        return 0;
    }
    uint256 totalAssets = epochs[requestId].totalAssetsSnapshotForDeposit + 1;
    uint256 totalSupply = epochs[requestId].totalSupplySnapshotForDeposit + 1;

    return assets.mulDiv(totalSupply, totalAssets, rounding);
}
```

Notice here that if the `requestId` matches the current [`epochId`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L107), the number of shares minted will be `0`.

With this necessary context, let's turn our attention to the implementation body of [`_claimDeposit(address,address)`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L742). Notice here, that a user will irreparably lose their entire deposit if they were to indirectly invoke this function whilst having a [`lastRequestId`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L751) that is equal to the current [`epochId`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L107):

```solidity
function _claimDeposit(
    address owner,
    address receiver
) internal returns (uint256 shares) {
    shares = previewClaimDeposit(owner); // 0

    uint256 lastRequestId = lastDepositRequestId[owner]; /// epochId
    uint256 assets = epochs[lastRequestId].depositRequestBalance[owner]; /// nonzero
    epochs[lastRequestId].depositRequestBalance[owner] = 0;
    _update(address(claimableSilo), receiver, shares);
    emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
}
```

## Impact

I am inclined to assess this issue as high severity for the following reasons:
1. A user can suffer a loss of their entire deposit.
2. There are multiple entry points to [`_claimDeposit(address,address)`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L742) which increases the likelihood of this occurence. See: [`claimAndRequestDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L204), [`claimAndRequestDepositWithPermit`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L327), [`claimDeposit`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L508C14-L508C26).

## Code Snippet

```solidity
function _claimDeposit(
    address owner,
    address receiver
) internal returns (uint256 shares) {
    shares = previewClaimDeposit(owner);

    uint256 lastRequestId = lastDepositRequestId[owner];
    uint256 assets = epochs[lastRequestId].depositRequestBalance[owner];
    epochs[lastRequestId].depositRequestBalance[owner] = 0;
    _update(address(claimableSilo), receiver, shares);
    emit ClaimDeposit(lastRequestId, owner, receiver, assets, shares);
}
```

## Tool used

Manual Review

## Recommendation

Ensure that attempts to [`_claimDeposit(address,address)`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L742) will `revert` if [`lastRequestId`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L751) is equal to the current [`epochId`](https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L107) and the `owner` has non-zero deposited `assets`.

