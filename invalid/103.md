Rough Taupe Beetle

medium

# If settle() is called after the vault was closed, but before it's opened, it won't be able to be opened again unless _maxDrawdown is manually reset by the owner

## Summary

Async vault can only be settled while the vault is closed, `_settle()` is called within the `open()` function but there's also an external `settle()` function which can be called in case the owners want to switch the epochs without needing to put all the funds back. The problem is that once an epoch has been settled through `settle()`, lastSavedBalance won't be set to 0 as its presumed and all further attempts to open the vault through `open()` will fail unless `_maxDrawdown` is reset manually by the owner.

## Vulnerability Detail

After a vault has been closed through the `close()` function, the `lastSavedBalance` is set as the `totalAssets()` s which is the current balance of the asset of the SyncSynthVault:

```solidity
  function close() external override onlyOwner {
        if (!vaultIsOpen) revert VaultIsClosed();

        if (totalAssets() == 0) revert VaultIsEmpty();

        lastSavedBalance = totalAssets();
        vaultIsOpen = false;
        _asset.safeTransfer(owner(), lastSavedBalance);
        emit EpochStart(block.timestamp, lastSavedBalance, totalSupply());
    }
```
When the owner wants to again either open the vault through `open()` or just settle the balance via the external `settle()` function, _settle will be called with the `newSavedBalance` OR `assetsReturned`. 

To prevent any mistyping of the newSavedBalance / assetsReturned there's a guardrail called `_maxDrawdown` which is currently set to 30%, this is to prevent any huge losses that might be caused by a human error. `_maxDrawdown` can be set anywhere from 0 to a 100%.

`uint16 internal _maxDrawdown; // guardrail`

When the owner decides to settle the vault through the `settle()` function before opening the vault due to multiple reasons, it's assumed that after the vault is settled, that the `lastSavedBalance` will be set to 0, but this is false. 

```solidity
  function settle(uint256 newSavedBalance) external {
        (uint256 lastSavedBalance, uint256 totalSupply) =
            _settle(newSavedBalance);
        lastSavedBalance = 0;
        emit EpochStart(block.timestamp, lastSavedBalance, totalSupply);
    }
```

The only thing which will be reset to 0, is the `lastSavedBalance` which is declared as the return value from the _settle function, but the global variable `lastSavedBalance` will be reset to the actual `lastSavedBalance` within the `_settle()` function: 

`lastSavedBalance = settleValues.lastSavedBalance`

When the owner decides to open the vault, the last savedBalance will be the same as in the epoch when the vault was closed, and they won't be able to open the vault if they try to pass a value which has more than a 30% difference with the `lastSavedBalance` due to the `_maxDrawdown`:

```solidity
  function _checkMaxDrawdown(
        uint256 _lastSavedBalance,
        uint256 newSavedBalance
    )
        internal
        view
    {
        if (
            newSavedBalance
                < _lastSavedBalance.mulDiv(
                    BPS_DIVIDER - _maxDrawdown, BPS_DIVIDER, Math.Rounding.Ceil
                )
        ) revert MaxDrawdownReached();
    }
```
This can be circumvented by manually resetting the `_maxDrawdown` value every time that this needs to be utilized through the `setMaxDrawdown` function which defeats the whole purpose of having a max drawdown. 

**PoC to confirm that lastSavedBalance won't be reset:**

The test can be inserted in the TestSettle.t.sol test suite within unit/concrete/sol tests.

```solidity

    function test_doubleSettle() public {
        close(vaultTested);
        uint256 lastSavedBalance = vaultTested.lastSavedBalance();
        console.log("Last Saved Balance: ", lastSavedBalance);
        vm.startPrank(vaultTested.owner());
        vaultTested.settle(lastSavedBalance);
        vm.stopPrank();
        uint256 lastSavedBalanceNew = vaultTested.lastSavedBalance();
        console.log("Last Saved Balance New: ", lastSavedBalanceNew);
    }

```

**PoC to confirm that open will revert due to maxDrawdownt:**

```solidity
    function test_doubleSettle() public {
        close(vaultTested);
        uint256 lastSavedBalance = vaultTested.lastSavedBalance();
        vm.startPrank(vaultTested.owner());
        vaultTested.settle(lastSavedBalance);

        vm.expectRevert();
        vaultTested.open(lastSavedBalance / 2);

        vm.stopPrank();
```

The amount used above in the `open()` function can be alternated with whatever value you'd like. 

## Impact

Once a a vault has been closed and then settled through the external `settle()` function, it won't be able to be opened again unless maxDrawdown is manually reset because the lastSavedValue won't be reset to 0, but will remain the previous one. Since the vault has been closed, no new deposits have been deposited to the sync vault in the meantime. 

## Code Snippet
https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L291
https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L307-L317
https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L348-L353
https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L913-L926
https://github.com/sherlock-audit/2024-03-amphor/blob/6c797025ffe296e04607abf74400ff2bb36a7de3/asynchronous-vault/src/AsyncSynthVault.sol#L852-L855
## Tool used

Manual Review

## Recommendation

Make sure that the global `lastSavedBalance` variable is reset to 0, or remove the external `settle` function in general. 
